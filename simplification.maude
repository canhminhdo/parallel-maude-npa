mod JOB-HANDLING is
    pr META-INTERPRETER .
    pr PARALLEL-CONFIG .
    pr MAP{Worker,IdSystemSet} .
    pr SET{Worker} .
    pr PARAMETER-GLOBAL .

    sorts J2J W2W .

    vars O MI : Oid .
    vars N C C1 C2 C3 C4 C5 : Nat .
    var Sess : Bound .
    vars IST IST' HistoryIST : IdSystemSet .
    vars IS IS' : IdSystem .
    vars W W' : Queue{Worker} .
    var W2J : Map{Worker,IdSystemSet} .

    op <_|_> : IdSystemSet IdSystemSet -> J2J .
    op <_,_> : Queue{Worker} Queue{Worker} -> W2W .

    *** get N jobs from a set of jobs if possible
    op getJobs : IdSystemSet Nat -> J2J .
    op getJobs* : IdSystemSet Nat IdSystemSet -> J2J .
    eq getJobs(IST, N) = getJobs*(IST, N, empty) .
    ceq getJobs*(IS IST, N, IST') = getJobs*(IST, sd(N, 1), IST' IS) if N > 0 .
    ceq getJobs*(empty, N, IST') = < empty | IST' > if N > 0 .
    eq getJobs*(IST, 0, IST') = < IST | IST' > .

    *** distribute a job to each worker in step 2.1
    op handleJobsOneByWorkers : Oid IdSystem Map{Worker,IdSystemSet} -> Configuration .
    eq handleJobsOneByWorkers(O, IS, (MI |-> IST, W2J))
    =   reduceTerm(MI, O,
            'MAUDE-NPA-EXT,
            'simplifyOne[
                upTerm(IS),
                upTerm(IST)
            ]
        )
        handleJobsOneByWorkers(O, IS, W2J) .
    eq handleJobsOneByWorkers(O, IS, empty) = none .

    *** distribute a job to each worker in step 2.2
    op handleOneHJobByWorkers : Oid IdSystem Map{Worker,IdSystemSet} -> Configuration .
    eq handleOneHJobByWorkers(O, IS, (MI |-> IST, W2J))
    =   reduceTerm(MI, O,
            'MAUDE-NPA-EXT,
            'simplifyHOne[
                upTerm(IST),
                upTerm(IS)
            ]
        )
        handleOneHJobByWorkers(O, IS, W2J) .
    eq handleOneHJobByWorkers(O, IS, empty) = none .

    *** calculate the number of workers for step 2.2 (simplificationH)
    op neededWorkersForSimH : Nat Nat Nat -> Nat .
    eq neededWorkersForSimH(C1, C2, N)
    =   if C1 * C2 < simBatchH
        then 1
        else neededWorkersForSimH*(C1, C2, N)
        fi .
    op neededWorkersForSimH* : Nat Nat Nat -> Nat .
    ceq neededWorkersForSimH*(C1, C2, N)
    =   if C5 < 1
        then 1
        else
            if C5 > 1
            then
                (if C5 > N then N else C5 fi)
            else
                (if (C2 rem C4) >= (C4 quo 2) then 2 else 1 fi)
            fi
        fi
    if C3 := (simBatchH quo C1)
    /\ C4 := (if C3 == 0 then 1 else C3 fi) --- the minimum number of states should be assigned to each worker
    /\ C5 := C2 quo C4 . --- #workers needed

    *** distribute a batch job to each worker in step 2.2
    op handleBatchJobsByWorkers : Oid Map{Worker,IdSystemSet} IdSystemSet -> Configuration .
    eq handleBatchJobsByWorkers(O, (MI |-> IST, W2J), HistoryIST)
    =   reduceTerm(MI, O,
            'MAUDE-NPA-EXT,
            'simplifyByImplicationH[
                upTerm(HistoryIST),
                upTerm(IST)
            ]
        )
        handleBatchJobsByWorkers(O, W2J, HistoryIST) .
    eq handleBatchJobsByWorkers(O, empty, HistoryIST) = none .

    *** pick up a needed number of workers from workers
    op getWorkers : Queue{Worker} Nat -> W2W .
    op getWorkers* : Queue{Worker} Nat Queue{Worker} -> W2W .
    eq getWorkers(W, N) = getWorkers*(W, N, emptyq) .
    eq getWorkers*(MI | W, N, W')
    =   if N > 0
        then getWorkers*(W, sd(N, 1), MI | W')
        else < MI | W, W' >
        fi .
    eq getWorkers*(emptyq, N, W') = < emptyq, W' > .

    *** divide jobs evenly to each worker
    op initW2J : Queue{Worker} -> Map{Worker,IdSystemSet} .
    eq initW2J(MI | W) =  MI |-> empty, initW2J(W) .
    eq initW2J(emptyq) = empty .

    op produceJobs2Workers : Queue{Worker} IdSystemSet -> Map{Worker,IdSystemSet} .
    eq produceJobs2Workers(W, IST) = produceJobs2Workers*(W, W, initW2J(W), IST) .

    op produceJobs2Workers* : Queue{Worker} Queue{Worker} Map{Worker,IdSystemSet} IdSystemSet -> Map{Worker,IdSystemSet} .
    eq produceJobs2Workers*(W, MI | W', (MI |-> IST', W2J), IS IST)
    = produceJobs2Workers*(W, W', (MI |-> (IS IST'), W2J), IST) .

    eq produceJobs2Workers*(W, emptyq, W2J, IST) = produceJobs2Workers*(W, W, W2J, IST) .
    eq produceJobs2Workers*(W, W', W2J, empty) = W2J .
endm

mod SIMPLIFICATION is
    pr JOB-HANDLING .

    vars O MI : Oid .
    var A : AttributeSet .
    vars N N1 C C1 C2 : Nat .
    var Sess : Bound .
    vars IST IST' IST'' IST1 IST2 IST3 INIT INIT' HistoryIST : IdSystemSet .
    vars IS : IdSystem .
    vars W W1 W2 : Queue{Worker} .
    var SJ : Queue{IdSystemSet} .
    vars T : Term .
    var RT : Type .
    var M : Module .
    var W2J : Map{Worker,IdSystemSet} .
    vars B B' : Bool .

    ************************************************************************
    *** parallelize step 2.1 (simplifyingL)                             ****
    ************************************************************************
    --- start the simplification in step 2.1
    crl [SimplificationLStart] : < O : Manager |
        status : startSimplifying,
        next : IST,
        underCheck : IST1,
        simJobs : IST2,
        current : IST3,
        A >
    => < O : Manager |
        status : simplifying,
        next : IST,
        underCheck : empty,
        simJobs : empty,
        current : empty,
        A >
    if C := #jobs(IST)
    [print "[SimplificationLStart] #jobs = " C] .

    crl [SimplificationLDone] : < O : Manager |
        status : simplifying,
        next : IST,
        underCheck : empty,
        simJobs : IST2,
        current : empty,
        A >
    => < O : Manager |
        status : startSimplifyingH,
        next : (IST' IST2),
        underCheck : empty,
        simJobs : empty,
        current : empty,
        A >
    if C := #jobs(IST)
    /\ C <= simBatch
    /\ IST' := simplifyByImplicationL(IST) 
    [print "[SimplificationLDone]"] .

    crl [SimOne] : < O : Manager |
        status : simplifying,
        workers : W,
        next : (IS IST),
        current : empty,
        underCheck : empty,
        rmFlag : B,
        A >
    => < O : Manager |
        status : simplifyingOne,
        workers : W,
        next : IST,
        current : IS,
        underCheck : empty,
        rmFlag : false,
        A >
    if C := #jobs(IS IST)
    /\ C > simBatch
    [print "[simplifyingOne]"] .

    crl [SimOneJob] : < O : Manager |
        status : simplifyingOne,
        #workers : N,
        workers : W,
        next : IST,
        current : IS,
        rmFlag : false,
        A >
    => < O : Manager |
        status : simplifyingOne,
        #workers : N,
        workers : W1,
        next : IST',
        current : IS,
        rmFlag : false,
        A >
        handleJobsOneByWorkers(O, IS, W2J)
    if qsize(W) == N
    /\ C := #jobs(IST) /\ C > 0
    /\ < W1, W2 > := getWorkers(W, C)
    /\ < IST' | IST'' > := getJobs(IST, qsize(W2) * jobSize) 
    /\ W2J := produceJobs2Workers(W2, IST'') .

    crl [Result4SimOne] : < O : Manager |
        status : simplifyingOne,
        workers : W,
        current : IS,
        underCheck : IST1,
        rmFlag : B,
        A >
        reducedTerm(O, MI, N, T, RT)
    => < O : Manager |
        status : simplifyingOne,
        workers : (W | MI),
        current : IS,
        underCheck : (IST IST1),
        rmFlag : (B or B'),
        A >
    if RT == 'ResSimPair
    /\ < B' | IST > := downTerm(T, errRes) .

    crl [SimOneFalseDone] : < O : Manager |
        status : simplifyingOne,
        #workers : N,
        workers : W,
        next : empty,
        current : IS,
        underCheck : IST1,
        simJobs : IST2,
        rmFlag : false,
        A >
    => < O : Manager |
        status : simplifying,
        #workers : N,
        workers : W,
        next : IST1,
        current : empty,
        underCheck : empty,
        simJobs : (IS IST2),
        rmFlag : false,
        A >
    if qsize(W) == N .

    crl [SimOneTrueDone] : < O : Manager |
        status : simplifyingOne,
        #workers : N,
        workers : W,
        next : IST,
        current : IS,
        underCheck : IST1,
        rmFlag : true,
        A >
    => < O : Manager |
        status : simplifying,
        #workers : N,
        workers : W,
        next : (IST1 IST),
        current : empty,
        underCheck : empty,
        rmFlag : false,
        A >
    if qsize(W) == N .

    ************************************************************************
    *** parallelize step 2.2 (SimplifyingH) - with memory optimization   ***
    ************************************************************************
    --- start the simplification in step 2.2 with history states
    crl [SimplificationHStart] : < O : Manager |
        status : startSimplifyingH,
        next : IST,
        history : HistoryIST,
        underCheck : IST1,
        simJobs : IST2,
        current : IST3,
        A >
    => < O : Manager |
        status : simplifyingH,
        next : IST,
        history : HistoryIST,
        underCheck : empty,
        simJobs : empty,
        current : empty,
        A >
    if memOpt == true
    /\ C1 := #jobs(HistoryIST)
    /\ C2 := #jobs(IST)
    [print "[SimplificationHStart] #His " C1 ", #jobs = " C2] .

    crl [SimplificationHDone] : < O : Manager |
        status : simplifyingH,
        history : HistoryIST,
        next : IST,
        underCheck : empty,
        simJobs : IST2,
        current : empty,
        A >
    => < O : Manager |
        status : startFiltering,
        history : HistoryIST,
        next : (IST' IST2),
        underCheck : empty,
        simJobs : empty,
        current : empty,
        A >
    if C1 := #jobs(IST)
    /\ C2 := #jobs(HistoryIST) 
    /\ C1 * C2 <= simBatchH
    /\ IST' := simplifyByImplicationH(HistoryIST, IST)
    [print "[SimplificationHDone]"] . 

    crl [SimHOne] : < O : Manager |
        status : simplifyingH,
        workers : W,
        history : HistoryIST,
        next : (IS IST),
        current : empty,
        underCheck : empty,
        A >
    => < O : Manager |
        status : simplifyingHOne,
        workers : W,
        history : HistoryIST,
        next : IST,
        current : IS,
        underCheck : empty,
        A >
    if C1 := #jobs(HistoryIST)
    /\ C2 := #jobs(IS IST)
    /\ C1 * C2 > simBatchH
    [print "[SimHOne]"] .

    crl [SimHOneJob] : < O : Manager |
        status : simplifyingHOne,
        #workers : N,
        workers : W,
        history : HistoryIST,
        current : IS,
        underCheck : IST1,
        rmFlag : false,
        A >
    => < O : Manager |
        status : simplifyingHOne,
        #workers : N,
        workers : W1,
        history : IST',
        current : IS,
        underCheck : (IST'' IST1),
        rmFlag : false,
        A >
        handleOneHJobByWorkers(O, IS, W2J)
    if qsize(W) == N
    /\ C := #jobs(HistoryIST) /\ C > 0
    /\ < W1, W2 > := getWorkers(W, C)
    /\ < IST' | IST'' > := getJobs(HistoryIST, qsize(W2) * jobHSize)
    /\ W2J := produceJobs2Workers(W2, IST'') .

    crl [Result4SimHOne] : < O : Manager |
        status : simplifyingHOne,
        workers : W,
        rmFlag : B,
        A >
        reducedTerm(O, MI, N, T, RT)
    => < O : Manager |
        status : simplifyingHOne,
        workers : (W | MI),
        rmFlag : (B or B'),
        A >
    if RT == 'Bool
    /\ B' := downTerm(T, errBool) .

    crl [SimHOneFalseDone] : < O : Manager |
        status : simplifyingHOne,
        #workers : N,
        workers : W,
        history : empty,
        current : IS,
        underCheck : IST1,
        simJobs : IST2,
        rmFlag : false,
        A >
    => < O : Manager |
        status : simplifyingH,
        #workers : N,
        workers : W,
        history : IST1,
        current : empty,
        underCheck : empty,
        simJobs : (IS IST2),
        rmFlag : false,
        A >
    if qsize(W) == N .

    crl [SimHOneTrueDone] : < O : Manager |
        status : simplifyingHOne,
        #workers : N,
        workers : W,
        history : HistoryIST,
        current : IS,
        underCheck : IST1,
        simJobs : IST2,
        rmFlag : true,
        A >
    => < O : Manager |
        status : simplifyingH,
        #workers : N,
        workers : W,
        history : (IST1 HistoryIST),
        current : empty,
        underCheck : empty,
        simJobs : IST2,
        rmFlag : false,
        A >
    if qsize(W) == N .

    ************************************************************************
    *** parallelize step 2.2 (SimplifyingH) with best performance        ***
    ************************************************************************
    crl [SimplificationHStart&Done] : < O : Manager |
        status : startSimplifyingH,
        workers : W,
        #workers : N,
        next : IST,
        history : HistoryIST,
        A >
    => < O : Manager |
        status : startFiltering,
        workers : W,
        #workers : N,
        next : IST',
        history : HistoryIST,
        A >
    if memOpt == false
    /\ C1 := #jobs(HistoryIST)
    /\ C2 := #jobs(IST)
    /\ C1 * C2 <= simBatchH
    --- /\ N1 := neededWorkersForSimH(C1, C2, N)
    --- /\ N1 <= 1
    /\ IST' := simplifyByImplicationH(HistoryIST, IST)
    /\ C := #jobs(IST')
    [print "[SimplificationHStart] #Hist = " C1 ", #jobs = " C2 "\n[SimplificationHDone] #jobs = " C] .

    crl [SimplificationHStart] : < O : Manager |
        status : startSimplifyingH,
        workers : W,
        #workers : N,
        next : IST,
        history : HistoryIST,
        A >
    => < O : Manager |
        status : (if C2 > 0 then simplifyingHBatch else stopping fi),
        workers : W1,
        #workers : N,
        next : empty,
        history : HistoryIST,
        A >
        if C2 > 0 then handleBatchJobsByWorkers(O, W2J, HistoryIST) else stopWorking(O, O) fi
    if memOpt == false
    /\ C1 := #jobs(HistoryIST)
    /\ C2 := #jobs(IST)
    /\ C1 * C2 > simBatchH
    --- /\ N1 := neededWorkersForSimH(C1, C2, N)
    --- /\ N1 > 1
    --- /\ < W1, W2 > := getWorkers(W, N1)
    /\ < W1, W2 > := getWorkers(W, C2)
    /\ C := qsize(W2)
    /\ W2J := produceJobs2Workers(W2, IST)
    [print "[SimplificationHStart] #usedInterpreters = " C ", #His " C1 ", #jobs = " C2] .

    crl [Result4SimHBatch] : < O : Manager |
        status : simplifyingHBatch,
        workers : W,
        next : IST,
        A >
        reducedTerm(O, MI, N, T, RT)
    => < O : Manager |
        status : simplifyingHBatch,
        workers : (W | MI),
        next : (IST IST'),
        A >
    if RT == 'IdSystemSet or-else RT == 'IdSystem
    /\ IST' := downTerm(T, errIdSystemSet)
    [print "[Result4SimHBatch]"] .

    crl [SimplificationHBatchDone] : < O : Manager |
        status : simplifyingHBatch,
        #workers : N,
        workers : W,
        next : IST,
        A >
    => < O : Manager |
        status : startFiltering,
        #workers : N,
        workers : W,
        next : IST,
        A >
    if qsize(W) == N
    /\ C := #jobs(IST)
    [print "[SimplificationHBatchDone] #jobs = " C] .

    ************************************************************************
    *** filtering states and initial states in step (3)                  ***
    ************************************************************************
    crl [FilterStates] : < O : Manager |
        status : startFiltering,
        #workers : N,
        workers : W,
        m : M,
        sess : Sess,
        init : INIT,
        jobs : empty,
        next : IST,
        history : HistoryIST,
        A >
    => < O : Manager |
        status : (if C > 0 then stopping else suspending fi),
        #workers : N,
        workers : W,
        m : M,
        sess : Sess,
        init : (INIT INIT'),
        jobs : IST',
        next : empty,
        history : (HistoryIST IST'),
        A >
        (if C > 0 then stopWorking(O, O) else none fi)
    if qsize(W) == N
    /\ IST =/= empty
    /\ < INIT', IST' > := searchState-CheckInit(M, Sess, HistoryIST, IST)
    /\ C := #jobs(INIT')
    /\ C1 := #jobs(IST')
    [print "<================= [Filter States and Initial States] #jobs = " C1 " #init = " C "] =================>"] .
endm